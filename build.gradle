buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.20'
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

plugins {
    id 'kotlin2js' version '1.3.20'
}

group 'be.anagon'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'com.moowork.node'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "${projectDir}/public/build/cells.js"
    kotlinOptions.moduleKind = "plain"
//    kotlinOptions.noStdlib = true
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

compileTestKotlin2Js.configure {
    kotlinOptions.moduleKind = "commonjs"
}

node {
    download = true
}
task installMocha(type: NpmTask) {
    args = ['install', 'mocha', 'kotlin', 'kotlin-test']
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    script = file('node_modules/mocha/bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha